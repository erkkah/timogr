plugins {
    id 'com.android.application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'timogr.app'
    compileSdk 30
    ndkVersion '21.0.6113669'

    defaultConfig {
        applicationId 'timogr.app'
        minSdk 26
        targetSdk 33
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }
        versionCode 2
        versionName '1.0.1'
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildFeatures {
        prefab true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            externalNativeBuild {
                cmake {
                    cFlags '-g'
                }
            }
            packagingOptions {
                doNotStrip '**.so'
            }
        }
    }
    externalNativeBuild {
        cmake {
            // version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation 'androidx.games:games-frame-pacing:1.9.0'
    implementation 'androidx.annotation:annotation:1.8.0'
}

task printAppID {
    println("APPID:${android.defaultConfig.applicationId}")
}
